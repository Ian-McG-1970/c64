
; todo movement - 
; if prev point moved to was 'empty'
;  if no direction selected
;    direction is previous direction
; endif
   
SCN = 2
SCN_BOT = SCN +2
REGA_BUF = SCN_BOT +2
REGX_BUF = REGA_BUF+1
REGY_BUF = REGX_BUF+1
REGA_INT = REGY_BUF+1
REGX_INT = REGA_INT+1
REGY_INT = REGX_INT+1

JOYX = REGY_INT +1 ; game speed
JOYY = JOYX +1
JOYF = JOYY +1
V_POS = JOYF +1
H_POS = V_POS +1

V_POS_BACKUP = H_POS +1
H_POS_BACKUP = V_POS_BACKUP +1

SCN_TOP = SCN

SCRN = $4000 ; $8000
SCRNBANK = $02 ; $01
BANK = $80

;START
*= 2049
!byte $0c,$08,$0a,$00,$9e   ; Line 10 SYS
!tx "2070"            ; Address for sys start in text 4096+11

*= 2070

SEI        ; disable maskable IRQs

 LDX #$FF  ; max stack pointer
 TXS

 LDA #$7F
 STA $DC0D  ; disable timer interrupts which can be generated by the two CIA chips
 STA $DD0D  ; the kernal uses such an interrupt to flash the cursor and scan the keyboard, so we better stop it.

 LDA $DC0D  ; by reading this two registers we negate any pending CIA irqs.
 LDA $DD0D  ; if we don't do this, a pending CIA irq might occur after we finish setting up our irq. we don't want that to happen.

 LDA #$01   ; this is how to tell the VICII to generate a raster interrupt
 STA $D01A

 LDA #$FA   ; this is how to tell at which rasterline we want the irq to be triggered
 STA $D012

 LDA #$1B   ; as there are more than 256 rasterlines, the topmost bit of $d011 serves as
 STA $D011  ; the 9th bit for the rasterline we want our irq to be triggered. here we simply set up a character screen, leaving the topmost bit 0.

 LDA #$35   ; we turn off the BASIC and KERNAL rom here
 STA $01    ; the cpu now sees RAM everywhere except at $d000-$e000, where still the registers of SID/VICII/etc are visible

 lDA #<BM_IRQ  ; this is how we set up
 STA $FFFE     ; the address of our interrupt code
 LDA #>BM_IRQ
 STA $FFFF
  
 LDA #<NMI_NOP  ; lsb
 STA $FFFA      ; Create a nop, irq handler for NMI that gets called whenever RESTORE is pressed or similar.
 LDA #>NMI_NOP  ; msb
 STA $FFFB      ; We're putting our irq handler directly in the vector that usually points to the kernal's NMI handler since we have kernal banked out.

 LDA #$00       ; Force an NMI by setting up a timer. This will cause an NMI, that won't be acked. Any subsequent NMI's from RESTORE will be essentially disabled.
 STA $DD0E      ; Stop timer A
 STA $DD04      ; Set timer A to 0, NMI will occure immediately after start
 STA $DD0E

 LDA #$81
 STA $DD0D      ; Set timer A as source for NMI

 LDA #$01
 STA $DD0E       ; Start timer A -> NMI

 LDA #SCRNBANK
 STA $DD00 ; bank
 
 LDA #BANK
 STA $D018    ;VIC Memory Control Register
    
 LDA #$00     ; screen and border
 STA $D020
; LDA #$05 ; $00
; STA $D021
; LDA #$0B ; $00
; STA $D022
; LDA #$0F ; $00
; STA $D023


 LDA #%00111011 ; 00111011 ; 76543210 - 7=MSBRST 6=ECM 5=BM 4=VIS 3=25/24 0-2=SCRL
 STA $D011    ;VIC Control Register 1
 LDA #%00001000 ; 0-2=SCRL 3=40/38 4=MCM 5-7=UNUSED
 STA $D016    ;VIC Control Register 2

 LDA #$CB ;$65 ; colour 01 (0000????) and colour 10 (????0000) - bank 1
 LDX #<(SCRN+$2000)
 LDY #>(SCRN+$2000)
 STX SCN+0
 STY SCN+1
 LDX #>1000 
 LDY #<1000
 JSR MEMSET

 LDA #$00 ;$65 ; colour 01 (0000????) and colour 10 (????0000) - bank 1
 LDX #<(SCRN)
 LDY #>(SCRN)
 STX SCN+0
 STY SCN+1
 LDX #>8000 
 LDY #<8000
 JSR MEMSET

LDA #20
STA H_POS
STA V_POS
 
;LDY #5
;LDX #6
;JSR PLOT10


CLI ; enable maskable interrupts again

MLOOP: JMP  MLOOP

!ALIGN 255,0
!ZONE DP_11000000
DP_11000000
  LDA SCR_TAB_HI,Y    ;4
  STA SCN_TOP+1       ;3 7
  LDA SCR_TAB_HI+1,Y    ;4 11
  STA SCN_BOT+1     ;3 14
  LDA SCR_TAB_LO,Y      ;4 18
  STA SCN_TOP     ;3 21
  LDA SCR_TAB_LO+1,Y      ;4 25
  STA SCN_BOT     ;3 28

  LDY HOR_TAB,X       ;4 32
  LDA #%11000000
  ORA (SCN_TOP),Y   ;5 37
  STA (SCN_TOP),Y   ;5 46
  LDA #%11000000
  ORA (SCN_BOT),Y   ;5 37
  STA (SCN_BOT),Y   ;5 51
  RTS

!ZONE DP_01100000
DP_01100000
  LDA SCR_TAB_HI,Y    ;4
  STA SCN_TOP+1       ;3 7
  LDA SCR_TAB_HI+1,Y    ;4 11
  STA SCN_BOT+1     ;3 14
  LDA SCR_TAB_LO,Y      ;4 18
  STA SCN_TOP     ;3 21
  LDA SCR_TAB_LO+1,Y      ;4 25
  STA SCN_BOT     ;3 28

  LDY HOR_TAB,X       ;4 32
  LDA #%01100000
  ORA (SCN_TOP),Y   ;5 37
  STA (SCN_TOP),Y   ;5 46
  LDA #%01100000
  ORA (SCN_BOT),Y   ;5 37
  STA (SCN_BOT),Y   ;5 51
  RTS

!ZONE DP_00110000
DP_00110000
  LDA SCR_TAB_HI,Y    ;4
  STA SCN_TOP+1       ;3 7
  LDA SCR_TAB_HI+1,Y    ;4 11
  STA SCN_BOT+1     ;3 14
  LDA SCR_TAB_LO,Y      ;4 18
  STA SCN_TOP     ;3 21
  LDA SCR_TAB_LO+1,Y      ;4 25
  STA SCN_BOT     ;3 28

  LDY HOR_TAB,X       ;4 32
  LDA #%00110000
  ORA (SCN_TOP),Y   ;5 37
  STA (SCN_TOP),Y   ;5 46
  LDA #%00110000
  ORA (SCN_BOT),Y   ;5 37
  STA (SCN_BOT),Y   ;5 51
  RTS

!ZONE DP_00011000
DP_00011000
  LDA SCR_TAB_HI,Y    ;4
  STA SCN_TOP+1       ;3 7
  LDA SCR_TAB_HI+1,Y    ;4 11
  STA SCN_BOT+1     ;3 14
  LDA SCR_TAB_LO,Y      ;4 18
  STA SCN_TOP     ;3 21
  LDA SCR_TAB_LO+1,Y      ;4 25
  STA SCN_BOT     ;3 28

  LDY HOR_TAB,X       ;4 32
  LDA #%00011000
  ORA (SCN_TOP),Y   ;5 37
  STA (SCN_TOP),Y   ;5 46
  LDA #%00011000
  ORA (SCN_BOT),Y   ;5 37
  STA (SCN_BOT),Y   ;5 51
  RTS

!ZONE DP_00001100
DP_00001100
  LDA SCR_TAB_HI,Y    ;4
  STA SCN_TOP+1       ;3 7
  LDA SCR_TAB_HI+1,Y    ;4 11
  STA SCN_BOT+1     ;3 14
  LDA SCR_TAB_LO,Y      ;4 18
  STA SCN_TOP     ;3 21
  LDA SCR_TAB_LO+1,Y      ;4 25
  STA SCN_BOT     ;3 28

  LDY HOR_TAB,X       ;4 32
  LDA #%00001100
  ORA (SCN_TOP),Y   ;5 37
  STA (SCN_TOP),Y   ;5 46
  LDA #%00001100
  ORA (SCN_BOT),Y   ;5 37
  STA (SCN_BOT),Y   ;5 51
  RTS

!ZONE DP_00000110
DP_00000110
  LDA SCR_TAB_HI,Y    ;4
  STA SCN_TOP+1       ;3 7
  LDA SCR_TAB_HI+1,Y    ;4 11
  STA SCN_BOT+1     ;3 14
  LDA SCR_TAB_LO,Y      ;4 18
  STA SCN_TOP     ;3 21
  LDA SCR_TAB_LO+1,Y      ;4 25
  STA SCN_BOT     ;3 28

  LDY HOR_TAB,X       ;4 32
  LDA #%00000110
  ORA (SCN_TOP),Y   ;5 37
  STA (SCN_TOP),Y   ;5 46
  LDA #%00000110
  ORA (SCN_BOT),Y   ;5 37
  STA (SCN_BOT),Y   ;5 51
  RTS

!ZONE DP_00000011
DP_00000011
  LDA SCR_TAB_HI,Y    ;4
  STA SCN_TOP+1       ;3 7
  LDA SCR_TAB_HI+1,Y    ;4 11
  STA SCN_BOT+1     ;3 14
  LDA SCR_TAB_LO,Y      ;4 18
  STA SCN_TOP     ;3 21
  LDA SCR_TAB_LO+1,Y      ;4 25
  STA SCN_BOT     ;3 28

  LDY HOR_TAB,X       ;4 32
  LDA #%00000011
  ORA (SCN_TOP),Y   ;5 37
  STA (SCN_TOP),Y   ;5 46
  LDA #%00000011
  ORA (SCN_BOT),Y   ;5 37
  STA (SCN_BOT),Y   ;5 51
  RTS

!ZONE DP_10000001
DP_10000001
  LDA SCR_TAB_HI,Y    ;4
  STA SCN_TOP+1       ;3 7
  LDA SCR_TAB_HI+1,Y    ;4 11
  STA SCN_BOT+1     ;3 14
  LDA SCR_TAB_LO,Y      ;4 18
  STA SCN_TOP     ;3 21
  LDA SCR_TAB_LO+1,Y      ;4 25
  STA SCN_BOT     ;3 28

  LDY HOR_TAB,X       ;4 32
  LDA #%00000001
  ORA (SCN_TOP),Y   ;5 37
  STA (SCN_TOP),Y   ;5 46
  LDA #%00000001
  ORA (SCN_BOT),Y   ;5 37
  STA (SCN_BOT),Y   ;5 51

  LDY HOR_TAB+8,X       ;4 32
  LDA #%10000000
  ORA (SCN_TOP),Y   ;5 37
  STA (SCN_TOP),Y   ;5 46
  LDA #%10000000
  ORA (SCN_BOT),Y   ;5 37
  STA (SCN_BOT),Y   ;5 51
  RTS

!ZONE DIR_UP
DIR_UP
;    LDY V_POS
    DEY
;    LDX H_POS
    JSR COLLIDE1
    BNE .EXIT
    LDY V_POS
    DEY
    LDX H_POS
    INX
    JSR COLLIDE1
    BNE .EXIT
      DEC V_POS
.EXIT RTS 
DIR_ER  RTS

!ZONE DIR_DN
DIR_DN
;    LDY V_POS
    INY
    INY
;    LDX H_POS
    JSR COLLIDE1
    BNE .EXIT
    LDY V_POS
    INY
    INY
    LDX H_POS
    INX
    JSR COLLIDE1
    BNE .EXIT
      INC V_POS
.EXIT RTS 

!ZONE DIR_RT
DIR_RT
;    LDY V_POS
;    LDX H_POS
    INX
    INX
    JSR COLLIDE1
    BNE .EXIT
    LDY V_POS
    INY
    LDX H_POS
    INX
    INX
    JSR COLLIDE1
    BNE .EXIT
      INC H_POS
.EXIT RTS 

!ZONE DIR_LT
DIR_LT
;    LDY V_POS
;    LDX H_POS
    DEX
    JSR COLLIDE1
    BNE .EXIT
    LDY V_POS
    INY
    LDX H_POS
    DEX
    JSR COLLIDE1
    BNE .EXIT
      DEC H_POS
.EXIT RTS 

!ZONE DIR_UL
DIR_UL
;    LDY V_POS
    DEY
;    LDX H_POS
    JSR COLLIDE1
    BNE .EXIT
    LDY V_POS
    DEY
    LDX H_POS
    DEX
    JSR COLLIDE1
    BNE .EXIT
    LDY V_POS
    LDX H_POS
    DEX
    JSR COLLIDE1
    BNE .EXIT
      DEC V_POS
      DEC H_POS
.EXIT RTS 

!ZONE DIR_DR
DIR_DR
;    LDY V_POS
    INY
;    LDX H_POS
    INX
    INX
    JSR COLLIDE1
    BNE .EXIT
    LDY V_POS
    INY
    INY
    LDX H_POS
    INX
    JSR COLLIDE1
    BNE .EXIT
    LDY V_POS
    INY
    INY
    LDX H_POS
    INX
    INX
    JSR COLLIDE1
    BNE .EXIT
      INC V_POS
      INC H_POS
.EXIT RTS 

!ZONE DIR_DL
DIR_DL
;    LDY V_POS
    INY
;    LDX H_POS
    DEX
    JSR COLLIDE1
    BNE .EXIT
    LDY V_POS
    INY
    INY
    LDX H_POS
    JSR COLLIDE1
    BNE .EXIT
    LDY V_POS
    INY
    INY
    LDX H_POS
    DEX
    JSR COLLIDE1
    BNE .EXIT
      INC V_POS
      DEC H_POS
.EXIT RTS 

!ZONE DIR_UR
DIR_UR
;    LDY V_POS
    DEY
;    LDX H_POS
    INX
    JSR COLLIDE1
    BNE .EXIT
    LDY V_POS
    LDX H_POS
    INX
    INX
    JSR COLLIDE1
    BNE .EXIT
    LDY V_POS
    DEY
    LDX H_POS
    INX
    INX
    JSR COLLIDE1
    BNE .EXIT
      DEC V_POS
      INC H_POS
.EXIT RTS

!ALIGN 255,0
!ZONE DC_11000000
DC_11000000
  LDA SCR_TAB_HI,Y    ;4
  STA SCN_TOP+1       ;3 7
  LDA SCR_TAB_HI+1,Y    ;4 11
  STA SCN_BOT+1     ;3 14
  LDA SCR_TAB_LO,Y      ;4 18
  STA SCN_TOP     ;3 21
  LDA SCR_TAB_LO+1,Y      ;4 25
  STA SCN_BOT     ;3 28

  LDY HOR_TAB,X       ;4 32
  LDA #%00111111
  AND (SCN_TOP),Y   ;5 37
  STA (SCN_TOP),Y   ;5 46
  LDA #%00111111
  AND (SCN_BOT),Y   ;5 37
  STA (SCN_BOT),Y   ;5 46
  RTS

!ZONE DC_01100000
DC_01100000
  LDA SCR_TAB_HI,Y    ;4
  STA SCN_TOP+1       ;3 7
  LDA SCR_TAB_HI+1,Y    ;4 11
  STA SCN_BOT+1     ;3 14
  LDA SCR_TAB_LO,Y      ;4 18
  STA SCN_TOP     ;3 21
  LDA SCR_TAB_LO+1,Y      ;4 25
  STA SCN_BOT     ;3 28

  LDY HOR_TAB,X       ;4 32
  LDA #%10011111
  AND (SCN_TOP),Y   ;5 37
  STA (SCN_TOP),Y   ;5 46
  LDA #%10011111
  AND (SCN_BOT),Y   ;5 37
  STA (SCN_BOT),Y   ;5 46
  RTS

!ZONE DC_00110000
DC_00110000
  LDA SCR_TAB_HI,Y    ;4
  STA SCN_TOP+1       ;3 7
  LDA SCR_TAB_HI+1,Y    ;4 11
  STA SCN_BOT+1     ;3 14
  LDA SCR_TAB_LO,Y      ;4 18
  STA SCN_TOP     ;3 21
  LDA SCR_TAB_LO+1,Y      ;4 25
  STA SCN_BOT     ;3 28

  LDY HOR_TAB,X       ;4 32
  LDA #%11001111
  AND (SCN_TOP),Y   ;5 37
  STA (SCN_TOP),Y   ;5 46
  LDA #%11001111
  AND (SCN_BOT),Y   ;5 37
  STA (SCN_BOT),Y   ;5 46
  RTS

!ZONE DC_00011000
DC_00011000
  LDA SCR_TAB_HI,Y    ;4
  STA SCN_TOP+1       ;3 7
  LDA SCR_TAB_HI+1,Y    ;4 11
  STA SCN_BOT+1     ;3 14
  LDA SCR_TAB_LO,Y      ;4 18
  STA SCN_TOP     ;3 21
  LDA SCR_TAB_LO+1,Y      ;4 25
  STA SCN_BOT     ;3 28

  LDY HOR_TAB,X       ;4 32
  LDA #%11100111
  AND (SCN_TOP),Y   ;5 37
  STA (SCN_TOP),Y   ;5 46
  LDA #%11100111
  AND (SCN_BOT),Y   ;5 37
  STA (SCN_BOT),Y   ;5 46
  RTS

!ZONE DC_00001100
DC_00001100
  LDA SCR_TAB_HI,Y    ;4
  STA SCN_TOP+1       ;3 7
  LDA SCR_TAB_HI+1,Y    ;4 11
  STA SCN_BOT+1     ;3 14
  LDA SCR_TAB_LO,Y      ;4 18
  STA SCN_TOP     ;3 21
  LDA SCR_TAB_LO+1,Y      ;4 25
  STA SCN_BOT     ;3 28

  LDY HOR_TAB,X       ;4 32
  LDA #%11110011
  AND (SCN_TOP),Y   ;5 37
  STA (SCN_TOP),Y   ;5 46
  LDA #%11110011
  AND (SCN_BOT),Y   ;5 37
  STA (SCN_BOT),Y   ;5 46
  RTS

!ZONE DC_00000110
DC_00000110
  LDA SCR_TAB_HI,Y    ;4
  STA SCN_TOP+1       ;3 7
  LDA SCR_TAB_HI+1,Y    ;4 11
  STA SCN_BOT+1     ;3 14
  LDA SCR_TAB_LO,Y      ;4 18
  STA SCN_TOP     ;3 21
  LDA SCR_TAB_LO+1,Y      ;4 25
  STA SCN_BOT     ;3 28

  LDY HOR_TAB,X       ;4 32
  LDA #%11111001
  AND (SCN_TOP),Y   ;5 37
  STA (SCN_TOP),Y   ;5 46
  LDA #%11111001
  AND (SCN_BOT),Y   ;5 37
  STA (SCN_BOT),Y   ;5 46
  RTS

!ZONE DC_00000011
DC_00000011
  LDA SCR_TAB_HI,Y    ;4
  STA SCN_TOP+1       ;3 7
  LDA SCR_TAB_HI+1,Y    ;4 11
  STA SCN_BOT+1     ;3 14
  LDA SCR_TAB_LO,Y      ;4 18
  STA SCN_TOP     ;3 21
  LDA SCR_TAB_LO+1,Y      ;4 25
  STA SCN_BOT     ;3 28

  LDY HOR_TAB,X       ;4 32
  LDA #%11111100
  AND (SCN_TOP),Y   ;5 37
  STA (SCN_TOP),Y   ;5 46
  LDA #%11111100
  AND (SCN_BOT),Y   ;5 37
  STA (SCN_BOT),Y   ;5 46
  RTS

!ZONE DC_10000001
DC_10000001
  LDA SCR_TAB_HI,Y    ;4
  STA SCN_TOP+1       ;3 7
  LDA SCR_TAB_HI+1,Y    ;4 11
  STA SCN_BOT+1     ;3 14
  LDA SCR_TAB_LO,Y      ;4 18
  STA SCN_TOP     ;3 21
  LDA SCR_TAB_LO+1,Y      ;4 25
  STA SCN_BOT     ;3 28

  LDY HOR_TAB,X       ;4 32
  LDA #%11111110
  AND (SCN_TOP),Y   ;5 37
  STA (SCN_TOP),Y   ;5 46
  LDA #%11111110
  AND (SCN_BOT),Y   ;5 37
  STA (SCN_BOT),Y   ;5 51

  LDY HOR_TAB+8,X       ;4 32
  LDA #%01111111
  AND (SCN_TOP),Y   ;5 37
  STA (SCN_TOP),Y   ;5 46
  LDA #%01111111
  AND (SCN_BOT),Y   ;5 37
  STA (SCN_BOT),Y   ;5 51
  RTS 

!ZONE DRAW_BOX
DRAW_BOX
    LDA BOX_TAB_LO,X
    STA .ADDR +1
.ADDR JMP DP_11000000

!ZONE CLEAR_BOX
CLEAR_BOX
    LDA BOX_TAB_LO,X
    STA .ADDR +1
.ADDR JMP DC_11000000

!ZONE JOYSTICK
JOYSTICK1 LDA $DC01     ; PORT 1
          JMP .JOYSTICK
JOYSTICK2 LDA $DC00     ; PORT 2
.JOYSTICK LDX #0
          LDY #0
.UP       LSR
          BCS   .DOWN
            DEY
.DOWN     LSR
          BCS   .LEFT
            INY
.LEFT     LSR
          BCS   .RIGHT
            DEX
.RIGHT    LSR
          BCS   .FIRE
            INX
.FIRE     EOR   #255
          AND   #1
          STA   JOYF
          STX   JOYX
          STY   JOYY
          LSR
          RTS

!ZONE PLOT0
PLOT0 LDA   SCR_TAB_HI,Y
      STA   SCN +1 
      LDA   SCR_TAB_LO,Y 
      STA   SCN 
      LDY   HOR_TAB,X
      LDA   (SCN),Y
      AND   AND_TAB,X
      STA   (SCN),Y 
        RTS

!ZONE PLOT1
PLOT1  LDA   SCR_TAB_HI,Y
        STA   SCN +1 
        LDA   SCR_TAB_LO,Y 
        STA   SCN 
        LDY   HOR_TAB,X
        LDA   (SCN),Y
        ORA   OR_TAB,X
        STA   (SCN),Y 
        RTS

V_DIR !BYTE 0
H_DIR !BYTE 0
V_SPD !BYTE 0
H_SPD !BYTE 0

!ZONE INERTIA
INERTIA
V_MOVE  LDA V_DIR
    BEQ .V_STIL
      CLC
      ADC V_SPD
      JMP .H_MOVE
.V_STIL LDA V_SPD
    BEQ .H_MOVE
    BMI .V_SP_N
.V_SP_P DEC V_SPD
    JMP .H_MOVE
.V_SP_N INC V_SPD
.H_MOVE LDA H_DIR
    BEQ .H_STIL
      CLC
      ADC H_SPD
      RTS
.H_STIL LDA H_SPD
    BEQ .EXIT
    BMI .H_SP_N
.H_SP_P DEC H_SPD
    RTS
.H_SP_N INC H_SPD   
.EXIT RTS

!ZONE MEMSET        
MEMSET       STY    .LSB_ONLY+1 ; store LSB count
             CPX    #0          ; MSB?     
             BEQ    .LSB_ONLY   ; no

             LDY    #0          ; yes so reset LSB
.MSB_LOOP  
.LSB_LOOP      STA    (SCN),Y   ; clear whole MSB
               DEY 
               BNE    .LSB_LOOP

              INC    SCN+1      ; inc MSB
              DEX               ; dec MSB count
              BNE    .MSB_LOOP

.LSB_ONLY    LDY    #0          ; LSB count 
             BEQ    .MS_END     ; not needed

.LAST_LSB_LOOP STA   (SCN),Y
               DEY 
               BNE   .LAST_LSB_LOOP
                
              STA   (SCN),Y     ; clear last Y (0)
 
.MS_END      RTS

!ZONE BM_IRQ
BM_IRQ:

    INC $D020
    STA REGA_INT
    STX REGX_INT
    STY REGY_INT

    JSR JOYSTICK2
    JSR CONVERT_MOVEMENT  ; convert directions to bitmap
    TAX
    LDA H_POS
    STA H_POS_BACKUP
    LDY V_POS
    STY V_POS_BACKUP
    JSR MOVEMENT      ; convert bits set to movement
    JSR CLEAR_DRAW_BOX

 LDA V_POS
 LDX #0
 LDY #18
 JSR HEX8
 LDA H_POS
 LDX #0
 LDY #24
 JSR HEX8
 
  LDA REGA_INT
  LDX REGX_INT
  LDY REGY_INT
  INC $D019    ;VIC Interrupt Request Register (IRR)
  DEC $D020

IRQ_EXIT: 
NMI_NOP:
RTI ; This is the irq handler for the NMI. Just returns without acknowledge. This prevents subsequent NMI's from interfering.

!ZONE CLEAR_DRAW_BOX
CLEAR_DRAW_BOX
    LDX H_POS_BACKUP
    LDY V_POS_BACKUP
    CPY V_POS
    BNE .DRAW
    CPX H_POS
    BEQ .SAME
.DRAW JSR CLEAR_BOX
      LDY V_POS
      LDX H_POS
      JSR DRAW_BOX
.SAME RTS

!ZONE CONVERT_MOVEMENT
CONVERT_MOVEMENT
    TXA
    BEQ .CONT
    BMI .NEGV
.POSV   LDA #1
      BNE .CONT ; JMP
.NEGV   LDA #2
.CONT TAX
    TYA
    BEQ .EXIT
    BMI .NEGH
.POSH   TXA
      ORA #4
      RTS
.NEGH   TXA
      ORA #8
      RTS
.EXIT TXA
    RTS

!ZONE MOVEMENT
MOVEMENT  ; pass in X/Y pos in A/Y and X holds direction and do movement and collision and returns position ix X/Y
    STA REGA_BUF
    LDA DIR_TAB_LO,X
    STA .ADDR+1
    LDX REGA_BUF
.ADDR JMP DIR_DN

!ZONE COLLIDE1
COLLIDE1  LDA   SCR_TAB_HI,Y
          STA   SCN +1 
          LDA   SCR_TAB_LO,Y 
          STA   SCN 
          LDY   HOR_TAB,X
          LDA   (SCN),Y
          AND   OR_TAB,X
          RTS
        
RANDOM  LDA   EORN+1
        BEQ   EORY
        ASL
        BEQ   EORN ;if the input was $80, skip the EOR
        BCC   EORN
EORY      EOR   #$1D
EORN    STA   EORN+1
        RTS

HEX16 STY   REGY_BUF  ; Y = YPOS / A = HI / X = LO
      STX   REGX_BUF    
      LDX   #0
      JSR   HEX8
      LDA   REGX_BUF
      LDY   REGY_BUF
      LDX   #16
;      JSR   HEX8
;      RTS

HEX8 ; A = NUM / Y = YPOS / X = XPOS

      STA   REGA_BUF
      LDA   SCR_TAB_LO+0,Y
      STA   HEX_NUMBER0+1
      
      LDA   SCR_TAB_HI+0,Y
      STA   HEX_NUMBER0+2

      LDA   SCR_TAB_LO+1,Y
      STA   HEX_NUMBER1+1
      
      LDA   SCR_TAB_HI+1,Y 
      STA   HEX_NUMBER1+2

      LDA   SCR_TAB_LO+2,Y
      STA   HEX_NUMBER2+1
      
      LDA   SCR_TAB_HI+2,Y 
      STA   HEX_NUMBER2+2

      LDA   SCR_TAB_LO+3,Y
      STA   HEX_NUMBER3+1
      
      LDA   SCR_TAB_HI+3,Y 
      STA   HEX_NUMBER3+2

      LDA   SCR_TAB_LO+4,Y
      STA   HEX_NUMBER4+1
      
      LDA   SCR_TAB_HI+4,Y 
      STA   HEX_NUMBER4+2

      LDA   REGA_BUF
      LSR
      LSR
      LSR
      LSR
      TAY
      JSR   HEXCHAR

      TXA
      CLC
      ADC   #8
      TAX

      LDA   REGA_BUF
      AND   #15
      TAY
;      JSR   HEXCHAR
;      RTS

HEXCHAR     LDA   HEX0,Y
HEX_NUMBER0 STA   $ABCD,X
      LDA   HEX1,Y
HEX_NUMBER1 STA   $ABCD,X
      LDA   HEX2,Y
HEX_NUMBER2 STA   $ABCD,X
      LDA   HEX3,Y
HEX_NUMBER3 STA   $ABCD,X
      LDA   HEX4,Y
HEX_NUMBER4 STA   $ABCD,X
      RTS

HEX0  !BYTE %00111110,%00001100,%01111110,%01111110,%01100011,%01111111,%00111111,%01111111,%00111110,%00111110,%00111110,%01111110,%00111110,%01111110,%00111110,%01111111 
HEX1  !BYTE %01100011,%00111100,%00000011,%00000011,%01100011,%01100000,%01100000,%00000011,%01100011,%01100011,%01100011,%01100011,%01100011,%01010011,%01100000,%01100000 
HEX2  !BYTE %01100011,%00001100,%00111110,%01111110,%00111111,%00111110,%00111110,%00000110,%00111110,%00111110,%01111111,%01111110,%01100000,%01100011,%00111110,%01111111
HEX3  !BYTE %01100011,%00001100,%01100000,%00000011,%00000011,%00000011,%01100011,%00001100,%01100011,%00000011,%01100011,%01100011,%01100011,%01100011,%01100000,%01100000
HEX4  !BYTE %00111110,%01111111,%01111111,%01111110,%00000011,%01111110,%00111110,%00011000,%00111110,%00111110,%01100011,%01111110,%00111110,%01111110,%01111110,%01100000

DIR_TAB_LO 
      !BYTE <DIR_ER ; 0000
      !BYTE <DIR_RT ; 0001
      !BYTE <DIR_LT ; 0010
      !BYTE <DIR_ER ; 0011
      !BYTE <DIR_DN ; 0100
      !BYTE <DIR_DR ; 0101
      !BYTE <DIR_DL ; 0110
      !BYTE <DIR_ER ; 0111
      !BYTE <DIR_UP ; 1000
      !BYTE <DIR_UR ; 1001
      !BYTE <DIR_UL ; 1010
      !BYTE <DIR_ER ; 1011
      !BYTE <DIR_ER ; 1100
      !BYTE <DIR_ER ; 1101
      !BYTE <DIR_ER ; 1110
      !BYTE <DIR_ER ; 1111

!ALIGN 255,0
SCR_TAB_HI
!for I = 0 TO 24
!BYTE >(SCRN+(I*320)+0),>(SCRN+(I*320)+1),>(SCRN+(I*320)+2),>(SCRN+(I*320)+3),>(SCRN+(I*320)+4),>(SCRN+(I*320)+5),>(SCRN+(I*320)+6),>(SCRN+(I*320)+7)
!end

!ALIGN 255,0
SCR_TAB_LO
!for I = 0 TO 24
!BYTE <(SCRN+(I*320)+0),<(SCRN+(I*320)+1),<(SCRN+(I*320)+2),<(SCRN+(I*320)+3),<(SCRN+(I*320)+4),<(SCRN+(I*320)+5),<(SCRN+(I*320)+6),<(SCRN+(I*320)+7)
!end

!ALIGN 255,0
OR_TAB:
!for I = 1 TO 32
!BYTE 128,64,32,16,8,4,2,1
!end

AND_TAB:
!for I = 1 TO 32
!BYTE 255-128,255-64,255-32,255-16,255-8,255-4,255-2,255-1
!end

HOR_TAB:
!FILL 8,00*8
!FILL 8,01*8
!FILL 8,02*8
!FILL 8,03*8
!FILL 8,04*8
!FILL 8,05*8
!FILL 8,06*8
!FILL 8,07*8
!FILL 8,08*8
!FILL 8,09*8
!FILL 8,10*8
!FILL 8,11*8
!FILL 8,12*8
!FILL 8,13*8
!FILL 8,14*8
!FILL 8,15*8
!FILL 8,16*8
!FILL 8,17*8
!FILL 8,18*8
!FILL 8,19*8
!FILL 8,20*8
!FILL 8,21*8
!FILL 8,22*8
!FILL 8,23*8
!FILL 8,24*8
!FILL 8,25*8
!FILL 8,26*8
!FILL 8,27*8
!FILL 8,28*8
!FILL 8,29*8
!FILL 8,30*8
!FILL 8,31*8

BOX_TAB_LO
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001
  !BYTE <DP_11000000,<DP_01100000,<DP_00110000,<DP_00011000,<DP_00001100,<DP_00000110,<DP_00000011,<DP_10000001

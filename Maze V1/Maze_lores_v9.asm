
; multicolour bitmap mode - %00 for background color 0 ($d021), %01 for the upper nibble of the screen matrix, %10 for lower nibble of the screen matrix and %11 for the lower nibble of color RAM.
   
BLACK = 0
WHITE = 1
RED   = 2
CYAN  = 3
PURPLE  = 4
GREEN = 5
BLUE  = 6
YELLOW  = 7
ORANGE  = 8
BROWN = 9
PINK  = 10
DARK_GRAY = 11
MID_GRAY  = 12
LIGHT_GREEN = 13
LIGHT_BLUE  = 14
LIGHT_GRAY  = 15

SCN = 2
SCN_BOT = SCN +2
DST_TOP = SCN_BOT +2
DST_BOT = DST_TOP +2
REGA_BUF = DST_BOT +2
REGX_BUF = REGA_BUF+1
REGY_BUF = REGX_BUF+1
REGA_INT = REGY_BUF+1
REGX_INT = REGA_INT+1
REGY_INT = REGX_INT+1

JOYX = REGY_INT +1 ; game speed
JOYY = JOYX +1
JOYF = JOYY +1
V_POS = JOYF +1
H_POS = V_POS +1

V_POS_BACKUP = H_POS +1
H_POS_BACKUP = V_POS_BACKUP +1

ROOM_CNT = H_POS_BACKUP +1
ROOM_START_H = ROOM_CNT +1
ROOM_START_V = ROOM_START_H +2
ROOM_END_HV = ROOM_START_V +2
ROOM_DIR = ROOM_END_HV +2
ROOM_POS = ROOM_DIR +2

ROOM_ST_H_VAL = ROOM_POS +1
ROOM_ST_V_VAL = ROOM_ST_H_VAL +1
ROOM_END_HV_VAL = ROOM_ST_V_VAL +1

SCN_TOP = SCN

START_V = JOYY
START_H = JOYX
END_VH = JOYF

SCRN = 40*1024; 8000 ; $4000
SCRN_BACKUP = SCRN + 16384
COLOUR = 35*1024
SCRNBANK = $01 ; $01
BANK = %00111000 ;$88

!MACRO COLL_SET V_OFFSET {
  LDA   SCR_TAB_HI +V_OFFSET,Y
    STA   SCN +1 
    LDA   SCR_TAB_LO +V_OFFSET,Y
    STA   SCN
}
!MACRO COLL_GET H_OFFSET {
    LDY   HOR_TAB +H_OFFSET,X
    LDA   (SCN),Y
    AND   OR_10_TAB +H_OFFSET,X ; allows shadowns to be overwritten
}

;START
*= 2049
!byte $0c,$08,$0a,$00,$9e   ; Line 10 SYS
!tx "2070"            ; Address for sys start in text 4096+11

*= 2070

SEI        ; disable maskable IRQs

 LDX #$FF  ; max stack pointer
 TXS

 LDA #$7F
 STA $DC0D  ; disable timer interrupts which can be generated by the two CIA chips
 STA $DD0D  ; the kernal uses such an interrupt to flash the cursor and scan the keyboard, so we better stop it.

 LDA $DC0D  ; by reading this two registers we negate any pending CIA irqs.
 LDA $DD0D  ; if we don't do this, a pending CIA irq might occur after we finish setting up our irq. we don't want that to happen.

 LDA #$01   ; this is how to tell the VICII to generate a raster interrupt
 STA $D01A

 LDA #$FA   ; this is how to tell at which rasterline we want the irq to be triggered
 STA $D012

 LDA #$1B   ; as there are more than 256 rasterlines, the topmost bit of $d011 serves as
 STA $D011  ; the 9th bit for the rasterline we want our irq to be triggered. here we simply set up a character screen, leaving the topmost bit 0.

 LDA #$35   ; we turn off the BASIC and KERNAL rom here
 STA $01    ; the cpu now sees RAM everywhere except at $d000-$e000, where still the registers of SID/VICII/etc are visible

 lDA #<BM_IRQ  ; this is how we set up
 STA $FFFE     ; the address of our interrupt code
 LDA #>BM_IRQ
 STA $FFFF
  
 LDA #<NMI_NOP  ; lsb
 STA $FFFA      ; Create a nop, irq handler for NMI that gets called whenever RESTORE is pressed or similar.
 LDA #>NMI_NOP  ; msb
 STA $FFFB      ; We're putting our irq handler directly in the vector that usually points to the kernal's NMI handler since we have kernal banked out.

 LDA #$00       ; Force an NMI by setting up a timer. This will cause an NMI, that won't be acked. Any subsequent NMI's from RESTORE will be essentially disabled.
 STA $DD0E      ; Stop timer A
 STA $DD04      ; Set timer A to 0, NMI will occure immediately after start
 STA $DD0E

 LDA #$81
 STA $DD0D      ; Set timer A as source for NMI

 LDA #$01
 STA $DD0E       ; Start timer A -> NMI

 LDA #SCRNBANK
 STA $DD00 ; bank
 
 LDA #BANK
 STA $D018    ;VIC Memory Control Register
    
 LDA #BLACK     ; screen and border
 STA $D020
 LDA #DARK_GRAY ; $00
 STA $D021
 LDA #MID_GRAY ; $00
 STA $D022
 LDA #LIGHT_GRAY ; $00
 STA $D023

 LDA #%00111011 ; 00111011 ; 76543210 - 7=MSBRST 6=ECM 5=BM 4=VIS 3=25/24 0-2=SCRL
 STA $D011    ;VIC Control Register 1
 LDA #%00011000 ; 0-2=SCRL 3=40/38 4=MCM 5-7=UNUSED
 STA $D016    ;VIC Control Register 2

 LDA #WHITE ; $01 ; colour 11 
 LDX #<$D800
 LDY #>$D800
 STX SCN+0
 STY SCN+1
 LDX #>1000 
 LDY #<1000
 JSR MEMSET

 LDA #(BLACK<<4)+LIGHT_GRAY ;$0B ;$65 ; colour 01 (0000????) and colour 10 (????0000) - bank 1
 LDX #<(COLOUR)
 LDY #>(COLOUR)
 STX SCN+0
 STY SCN+1
 LDX #>1000 
 LDY #<1000
 JSR MEMSET

  JSR SCN_SETUP
  
LDA #20
STA H_POS
STA V_POS
 
  LDX #<SCRN
  LDY #>SCRN
  STX SCN+0
  STY SCN+1
  LDX #<SCRN_BACKUP
  LDY #>SCRN_BACKUP
  STX DST_TOP+0
  STY DST_TOP+1
  LDY #<8000
  LDX #>8000
  JSR MEMCPY

CLI ; enable maskable interrupts again

MLOOP: JMP  MLOOP

!ZONE DIR_UP
DIR_UP
  +COLL_SET -1
  +COLL_GET 0
    BNE .EXIT
      DEC V_POS
.EXIT RTS 
DIR_ER  RTS

!ZONE DIR_DN
DIR_DN
  +COLL_SET 2
  +COLL_GET 0
    BNE .EXIT
      INC V_POS
.EXIT RTS 

!ZONE DIR_RT
DIR_RT
  +COLL_SET 0
  +COLL_GET 1
    BNE .EXIT
    LDY V_POS
    LDX H_POS
  +COLL_SET 1
  +COLL_GET 1
    BNE .EXIT
      INC H_POS
.EXIT RTS 

!ZONE DIR_LT
DIR_LT
  +COLL_SET 0
  +COLL_GET -1
    BNE .EXIT
    LDY V_POS
    LDX H_POS
  +COLL_SET 1
  +COLL_GET -1
    BNE .EXIT
      DEC H_POS
.EXIT RTS 

!ZONE DIR_UL
DIR_UL
  +COLL_SET -1
  +COLL_GET -1
    BNE .EXIT
    LDY V_POS
    LDX H_POS
  +COLL_SET 0
  +COLL_GET -1
    BNE .EXIT
      DEC V_POS
      DEC H_POS
.EXIT RTS 

!ZONE DIR_DR
DIR_DR
  +COLL_SET 1
  +COLL_GET 1
    BNE .EXIT
    LDY V_POS
    LDX H_POS
  +COLL_SET 2
  +COLL_GET 1
    BNE .EXIT
      INC V_POS
      INC H_POS
.EXIT RTS 

!ZONE DIR_DL
DIR_DL
  +COLL_SET 1
  +COLL_GET -1
    BNE .EXIT
    LDY V_POS
    LDX H_POS
  +COLL_SET 2
  +COLL_GET -1
    BNE .EXIT
      INC V_POS
      DEC H_POS
.EXIT RTS 

!ZONE DIR_UR
DIR_UR
  +COLL_SET -1
  +COLL_GET 1
    BNE .EXIT
    LDY V_POS
    LDX H_POS
  +COLL_SET 0
  +COLL_GET 1
    BNE .EXIT
      DEC V_POS
      INC H_POS
.EXIT RTS

!ZONE JOYSTICK
JOYSTICK1 LDA $DC01     ; PORT 1
          JMP .JOYSTICK
JOYSTICK2 LDA $DC00     ; PORT 2
.JOYSTICK LDX #0
          LDY #0
.UP       LSR
          BCS   .DOWN
            DEY
.DOWN     LSR
          BCS   .LEFT
            INY
.LEFT     LSR
          BCS   .RIGHT
            DEX
.RIGHT    LSR
          BCS   .FIRE
            INX
.FIRE     EOR   #255
          AND   #1
          STA   JOYF
          STX   JOYX
          STY   JOYY
          LSR
          RTS

!ZONE PLOT_00
PLOT_00
      LDA   SCR_TAB_HI,Y
      STA   SCN +1 
      LDA   SCR_TAB_LO,Y 
      STA   SCN 
      LDY   HOR_TAB,X
      LDA   (SCN),Y
      AND   AND_00_TAB,X
      STA   (SCN),Y 
      RTS

!ZONE PLOT_01
PLOT_01
      LDA   SCR_TAB_HI,Y
      STA   SCN +1 
      LDA   SCR_TAB_LO,Y 
      STA   SCN 
      LDY   HOR_TAB,X
      LDA   (SCN),Y
      ORA   OR_01_TAB,X
      STA   (SCN),Y 
      RTS

!ZONE PLOT_10
PLOT_10
      LDA   SCR_TAB_HI,Y
      STA   SCN +1 
      LDA   SCR_TAB_LO,Y 
      STA   SCN 
      LDY   HOR_TAB,X
      LDA   (SCN),Y
      ORA   OR_10_TAB,X
      STA   (SCN),Y 
      RTS

!ZONE PLOT_11
PLOT_11
      LDA   SCR_TAB_HI,Y
      STA   SCN +1 
      LDA   SCR_TAB_LO,Y 
      STA   SCN 
      LDY   HOR_TAB,X
      LDA   (SCN),Y
      ORA   OR_11_TAB,X
      STA   (SCN),Y 
      RTS

!ALIGN 255,0
!ZONE BOX_11_01_06
BOX_11_01_06
      LDA   SCR_TAB_HI,Y    ;4
      STA   SCN +1        ;3
      LDA   SCR_TAB_LO,Y    ;4
      STA   SCN         ;3

      LDY   HOR_TAB,X     ;4
      LDA   (SCN),Y       ;5
      ORA   OR_11_TAB,X     ;4
      STA   (SCN),Y       ;6
      INY           ;2
      LDA   (SCN),Y       ;5
      ORA   OR_11_TAB,X     ;4
      STA   (SCN),Y       ;6

      LDY   HOR_TAB+1,X     ;4
      INY           ;2
      LDA   (SCN),Y       ;5
; check if shadow should be drawn
      ORA   OR_01_TAB+1,X   ;4
      STA   (SCN),Y       ;6
      INY           ;2
      LDA   (SCN),Y       ;5
; check if shadow should be drawn
      ORA   OR_01_TAB+1,X   ;4
      STA   (SCN),Y       ;6

      RTS

!ZONE BOX_11_07_08
BOX_11_07_08
      LDA   SCR_TAB_HI,Y    ;4
      STA   SCN +1        ;3
      LDA   SCR_TAB_LO,Y    ;4
      STA   SCN         ;3

      LDA   SCR_TAB_HI+2,Y    ;4
      STA   DST_BOT +1      ;3
      LDA   SCR_TAB_LO+2,Y    ;4
      STA   DST_BOT       ;3

      LDY   HOR_TAB,X     ;2
      LDA   (SCN),Y       ;5
      ORA   OR_11_TAB,X     ;4
      STA   (SCN),Y       ;6
      INY           ;2
      LDA   (SCN),Y       ;5
      ORA   OR_11_TAB,X     ;4
      STA   (SCN),Y       ;6

      LDY   HOR_TAB+1,X     ;4
      LDA   (DST_BOT),Y     ;5
; check if shadow should be drawn
      ORA   OR_01_TAB+1,X   ;4
      STA   (DST_BOT),Y     ;6

      INY           ;2
      LDA   (SCN),Y       ;5
; check if shadow should be drawn
      ORA   OR_01_TAB+1,X   ;4
      STA   (SCN),Y       ;6
      RTS

!ZONE BOX_11_08_08
BOX_11_08_08
      LDA   SCR_TAB_HI,Y    ;4
      STA   SCN +1        ;3
      LDA   SCR_TAB_LO,Y    ;4
      STA   SCN         ;3
      LDA   SCR_TAB_HI+1,Y    ;4
      STA   DST_BOT +1      ;3
      LDA   SCR_TAB_LO+1,Y    ;4
      STA   DST_BOT       ;3

      LDY   HOR_TAB,X     ;4
      LDA   (SCN),Y       ;5
      ORA   OR_11_TAB,X     ;4
      STA   (SCN),Y       ;6
      LDA   (DST_BOT),Y     ;5
      ORA   OR_11_TAB,X     ;4
      STA   (DST_BOT),Y     ;6

      LDY   HOR_TAB+1,X     ;4
      LDA   (DST_BOT),Y     ;5
; check if shadow should be drawn
      ORA   OR_01_TAB+1,X   ;4
      STA   (DST_BOT),Y     ;6
      INY           ;2
      LDA   (DST_BOT),Y     ;5
; check if shadow should be drawn
      ORA   OR_01_TAB+1,X   ;4
      STA   (DST_BOT),Y     ;6

      RTS

!ZONE BULLET_11_01_07
BULLET_11_01_07
      LDA   SCR_TAB_HI,Y    ;4
      STA   SCN +1        ;3
      LDA   SCR_TAB_LO,Y    ;4
      STA   SCN         ;3

      LDY   HOR_TAB,X     ;4
      LDA   (SCN),Y       ;5
      ORA   OR_11_TAB,X     ;4
      STA   (SCN),Y       ;6

      LDY   HOR_TAB+1,X     ;4
      INY           ;2
      LDA   (SCN),Y       ;5
      ORA   OR_01_TAB+1,X   ;4
      STA   (SCN),Y       ;6

      RTS

!ZONE BULLET_11_08_08
BULLET_11_08_08
      LDA   SCR_TAB_HI,Y    ;4
      STA   SCN +1        ;3
      LDA   SCR_TAB_LO,Y    ;4
      STA   SCN         ;3

      LDA   SCR_TAB_HI+1,Y    ;4
      STA   DST_BOT +1      ;3
      LDA   SCR_TAB_LO+1,Y    ;4
      STA   DST_BOT       ;3

      LDY   HOR_TAB,X     ;2
      LDA   (SCN),Y       ;5
      ORA   OR_11_TAB,X     ;4
      STA   (SCN),Y       ;6

      LDY   HOR_TAB+1,X     ;4
      LDA   (DST_BOT),Y     ;5
      ORA   OR_01_TAB+1,X   ;4
      STA   (DST_BOT),Y     ;6

      RTS

!ALIGN 255,0
!ZONE BOX_10_01_06
BOX_10_01_06
      LDA   SCR_TAB_HI,Y    ;4
      STA   SCN +1        ;3
      LDA   SCR_TAB_LO,Y    ;4
      STA   SCN         ;3

      LDY   HOR_TAB,X     ;4
      LDA   (SCN),Y       ;5
      ORA   OR_10_TAB,X     ;4
      STA   (SCN),Y       ;6
      INY           ;2
      LDA   (SCN),Y       ;5
      ORA   OR_10_TAB,X     ;4
      STA   (SCN),Y       ;6

      LDY   HOR_TAB+1,X     ;4
      INY           ;2
      LDA   (SCN),Y       ;5
; check if shadow should be drawn
      ORA   OR_01_TAB+1,X   ;4
      STA   (SCN),Y       ;6
      INY           ;2
      LDA   (SCN),Y       ;5
; check if shadow should be drawn
      ORA   OR_01_TAB+1,X   ;4
      STA   (SCN),Y       ;6

      RTS

!ZONE BOX_10_07_08
BOX_10_07_08
      LDA   SCR_TAB_HI,Y    ;4
      STA   SCN +1        ;3
      LDA   SCR_TAB_LO,Y    ;4
      STA   SCN         ;3

      LDA   SCR_TAB_HI+2,Y    ;4
      STA   DST_BOT +1      ;3
      LDA   SCR_TAB_LO+2,Y    ;4
      STA   DST_BOT       ;3

      LDY   HOR_TAB,X     ;2
      LDA   (SCN),Y       ;5
      ORA   OR_10_TAB,X     ;4
      STA   (SCN),Y       ;6
      INY           ;2
      LDA   (SCN),Y       ;5
      ORA   OR_10_TAB,X     ;4
      STA   (SCN),Y       ;6

      LDY   HOR_TAB+1,X     ;4
      LDA   (DST_BOT),Y     ;5
; check if shadow should be drawn
      ORA   OR_01_TAB+1,X   ;4
      STA   (DST_BOT),Y     ;6

      INY           ;2
      LDA   (SCN),Y       ;5
; check if shadow should be drawn
      ORA   OR_01_TAB+1,X   ;4
      STA   (SCN),Y       ;6
      RTS

!ZONE BOX_10_08_08
BOX_10_08_08
      LDA   SCR_TAB_HI,Y    ;4
      STA   SCN +1        ;3
      LDA   SCR_TAB_LO,Y    ;4
      STA   SCN         ;3
      LDA   SCR_TAB_HI+1,Y    ;4
      STA   DST_BOT +1      ;3
      LDA   SCR_TAB_LO+1,Y    ;4
      STA   DST_BOT       ;3

      LDY   HOR_TAB,X     ;4
      LDA   (SCN),Y       ;5
      ORA   OR_10_TAB,X     ;4
      STA   (SCN),Y       ;6
      LDA   (DST_BOT),Y     ;5
      ORA   OR_10_TAB,X     ;4
      STA   (DST_BOT),Y     ;6

      LDY   HOR_TAB+1,X     ;4
      LDA   (DST_BOT),Y     ;5
; check if shadow should be drawn
      ORA   OR_01_TAB+1,X   ;4
      STA   (DST_BOT),Y     ;6
      INY           ;2
      LDA   (DST_BOT),Y     ;5
; check if shadow should be drawn
      ORA   OR_01_TAB+1,X   ;4
      STA   (DST_BOT),Y     ;6

      RTS

!ZONE BULLET_10_01_07
BULLET_10_01_07
      LDA   SCR_TAB_HI,Y    ;4
      STA   SCN +1        ;3
      LDA   SCR_TAB_LO,Y    ;4
      STA   SCN         ;3

      LDY   HOR_TAB,X     ;4
      LDA   (SCN),Y       ;5
      ORA   OR_10_TAB,X     ;4
      STA   (SCN),Y       ;6

      LDY   HOR_TAB+1,X     ;4
      INY           ;2
      LDA   (SCN),Y       ;5
      ORA   OR_01_TAB+1,X   ;4
      STA   (SCN),Y       ;6

      RTS

!ZONE BULLET_10_08_08
BULLET_10_08_08
      LDA   SCR_TAB_HI,Y    ;4
      STA   SCN +1        ;3
      LDA   SCR_TAB_LO,Y    ;4
      STA   SCN         ;3

      LDA   SCR_TAB_HI+1,Y    ;4
      STA   DST_BOT +1      ;3
      LDA   SCR_TAB_LO+1,Y    ;4
      STA   DST_BOT       ;3

      LDY   HOR_TAB,X     ;2
      LDA   (SCN),Y       ;5
      ORA   OR_10_TAB,X     ;4
      STA   (SCN),Y       ;6

      LDY   HOR_TAB+1,X     ;4
      LDA   (DST_BOT),Y     ;5
      ORA   OR_01_TAB+1,X   ;4
      STA   (DST_BOT),Y     ;6

      RTS

!ALIGN 255,0
!ZONE BOX_01_01_06
BOX_01_01_06
      LDA   SCR_TAB_HI,Y    ;4
      STA   SCN +1        ;3
      LDA   SCR_TAB_LO,Y    ;4
      STA   SCN         ;3

      LDY   HOR_TAB,X     ;4
      LDA   (SCN),Y       ;5
      ORA   OR_01_TAB,X     ;4
      STA   (SCN),Y       ;6
      INY           ;2
      LDA   (SCN),Y       ;5
      ORA   OR_01_TAB,X     ;4
      STA   (SCN),Y       ;6

      LDY   HOR_TAB+1,X     ;4
      INY           ;2
      LDA   (SCN),Y       ;5
; check if shadow should be drawn
      ORA   OR_01_TAB+1,X   ;4
      STA   (SCN),Y       ;6
      INY           ;2
      LDA   (SCN),Y       ;5
; check if shadow should be drawn
      ORA   OR_01_TAB+1,X   ;4
      STA   (SCN),Y       ;6

      RTS

!ZONE BOX_01_07_08
BOX_01_07_08
      LDA   SCR_TAB_HI,Y    ;4
      STA   SCN +1        ;3
      LDA   SCR_TAB_LO,Y    ;4
      STA   SCN         ;3

      LDA   SCR_TAB_HI+2,Y    ;4
      STA   DST_BOT +1      ;3
      LDA   SCR_TAB_LO+2,Y    ;4
      STA   DST_BOT       ;3

      LDY   HOR_TAB,X     ;2
      LDA   (SCN),Y       ;5
      ORA   OR_01_TAB,X     ;4
      STA   (SCN),Y       ;6
      INY           ;2
      LDA   (SCN),Y       ;5
      ORA   OR_01_TAB,X     ;4
      STA   (SCN),Y       ;6

      LDY   HOR_TAB+1,X     ;4
      LDA   (DST_BOT),Y     ;5
; check if shadow should be drawn
      ORA   OR_01_TAB+1,X   ;4
      STA   (DST_BOT),Y     ;6

      INY           ;2
      LDA   (SCN),Y       ;5
; check if shadow should be drawn
      ORA   OR_01_TAB+1,X   ;4
      STA   (SCN),Y       ;6
      RTS

!ZONE BOX_01_08_08
BOX_01_08_08
      LDA   SCR_TAB_HI,Y    ;4
      STA   SCN +1        ;3
      LDA   SCR_TAB_LO,Y    ;4
      STA   SCN         ;3
      LDA   SCR_TAB_HI+1,Y    ;4
      STA   DST_BOT +1      ;3
      LDA   SCR_TAB_LO+1,Y    ;4
      STA   DST_BOT       ;3

      LDY   HOR_TAB,X     ;4
      LDA   (SCN),Y       ;5
      ORA   OR_01_TAB,X     ;4
      STA   (SCN),Y       ;6
      LDA   (DST_BOT),Y     ;5
      ORA   OR_01_TAB,X     ;4
      STA   (DST_BOT),Y     ;6

      LDY   HOR_TAB+1,X     ;4
      LDA   (DST_BOT),Y     ;5
; check if shadow should be drawn
      ORA   OR_01_TAB+1,X   ;4
      STA   (DST_BOT),Y     ;6
      INY           ;2
      LDA   (DST_BOT),Y     ;5
; check if shadow should be drawn
      ORA   OR_01_TAB+1,X   ;4
      STA   (DST_BOT),Y     ;6

      RTS

!ALIGN 255,0
!ZONE CLR_00_01_06
CLR_00_01_06
      LDA   SCR_TAB_HI,Y
      STA   DST_TOP +1 
      ORA   #64
      STA   SCN +1
      LDA   SCR_TAB_LO,Y 
      STA   DST_TOP 
      STA   SCN

      LDY   HOR_TAB,X
      LDA   (SCN),Y
      STA   (DST_TOP),Y
      INY
      LDA   (SCN),Y
      STA   (DST_TOP),Y
      INY
      LDA   (SCN),Y
      STA   (DST_TOP),Y
      
      LDY   HOR_TAB+1,X
      LDA   (SCN),Y
      STA   (DST_TOP),Y
      INY
      LDA   (SCN),Y
      STA   (DST_TOP),Y
      INY
      LDA   (SCN),Y
      STA   (DST_TOP),Y

      RTS

!ZONE CLR_00_07_08
CLR_00_07_08
      LDA   SCR_TAB_HI,Y
      STA   DST_TOP +1 
      ORA   #64
      STA   SCN +1
      LDA   SCR_TAB_LO,Y 
      STA   DST_TOP 
      STA   SCN
      LDA   SCR_TAB_HI+2,Y    ;4
      STA   DST_BOT +1      ;3
      ORA   #64
      STA   SCN_BOT +1
      LDA   SCR_TAB_LO+2,Y    ;4
      STA   DST_BOT       ;3
      STA   SCN_BOT

      LDY   HOR_TAB,X
      LDA   (SCN),Y
      STA   (DST_TOP),Y
      LDA   (SCN_BOT),Y
      STA   (DST_BOT),Y
      INY
      LDA   (SCN),Y
      STA   (DST_TOP),Y
      
      LDY   HOR_TAB+1,X
      LDA   (SCN),Y
      STA   (DST_TOP),Y
      LDA   (SCN_BOT),Y
      STA   (DST_BOT),Y
      INY
      LDA   (SCN),Y
      STA   (DST_TOP),Y

      RTS

!ZONE CLR_00_08_08
CLR_00_08_08
      LDA   SCR_TAB_HI,Y
      STA   DST_TOP +1 
      ORA   #64
      STA   SCN +1
      LDA   SCR_TAB_LO,Y 
      STA   DST_TOP 
      STA   SCN
      LDA   SCR_TAB_HI+1,Y    ;4
      STA   DST_BOT +1      ;3
      ORA   #64
      STA   SCN_BOT +1
      LDA   SCR_TAB_LO+1,Y    ;4
      STA   DST_BOT       ;3
      STA   SCN_BOT

      LDY   HOR_TAB,X
      LDA   (SCN),Y
      STA   (DST_TOP),Y
      LDA   (SCN_BOT),Y
      STA   (DST_BOT),Y
      INY
      LDA   (SCN_BOT),Y
      STA   (DST_BOT),Y
      
      LDY   HOR_TAB+1,X
      LDA   (SCN),Y
      STA   (DST_TOP),Y
      LDA   (SCN_BOT),Y
      STA   (DST_BOT),Y
      INY
      LDA   (SCN_BOT),Y
      STA   (DST_BOT),Y

      RTS


V_DIR !BYTE 0
H_DIR !BYTE 0
V_SPD !BYTE 0
H_SPD !BYTE 0

!ZONE INERTIA
INERTIA
V_MOVE  LDA V_DIR
    BEQ .V_STIL
      CLC
      ADC V_SPD
      JMP .H_MOVE
.V_STIL LDA V_SPD
    BEQ .H_MOVE
    BMI .V_SP_N
.V_SP_P DEC V_SPD
    JMP .H_MOVE
.V_SP_N INC V_SPD
.H_MOVE LDA H_DIR
    BEQ .H_STIL
      CLC
      ADC H_SPD
      RTS
.H_STIL LDA H_SPD
    BEQ .EXIT
    BMI .H_SP_N
.H_SP_P DEC H_SPD
    RTS
.H_SP_N INC H_SPD   
.EXIT RTS

!ZONE MEMSET        
MEMSET       STY    .LSB_ONLY+1 ; store LSB count
             CPX    #0          ; MSB?     
             BEQ    .LSB_ONLY   ; no

             LDY    #0          ; yes so reset LSB
.MSB_LOOP  
.LSB_LOOP      STA    (SCN),Y   ; clear whole MSB
               DEY 
               BNE    .LSB_LOOP

              INC    SCN+1      ; inc MSB
              DEX               ; dec MSB count
              BNE    .MSB_LOOP

.LSB_ONLY    LDY    #0          ; LSB count 
             BEQ    .MS_END     ; not needed

.LAST_LSB_LOOP STA   (SCN),Y
               DEY 
               BNE   .LAST_LSB_LOOP
                
              STA   (SCN),Y     ; clear last Y (0)
 
.MS_END      RTS

;move memory down
;
; FROM = source start address
;   TO = destination start address
; SIZE = number of bytes to move
  
!ZONE MEMCPY
MEMCPY
    STY .LSB +1
    LDY #0
    TXA
    BEQ .LSB
.LOOPHI LDA (SCN),Y ; move a page at a time
        STA (DST_TOP),Y
        INY
        BNE .LOOPHI
      INC SCN +1
      INC DST_TOP+1
      DEX
      BNE .LOOPHI
.LSB    LDX #0
        BEQ .EXIT
.LOOPLO   LDA (SCN),Y ; move the remaining bytes
          STA (DST_TOP),Y
          INY
          DEX
          BNE .LOOPLO
.EXIT  RTS

!ZONE BM_IRQ
BM_IRQ:

    INC $D020
    STA REGA_INT
    STX REGX_INT
    STY REGY_INT

    JSR JOYSTICK2
    JSR CONVERT_MOVEMENT  ; convert directions to bitmap
;    TAX
    LDX H_POS
    STX H_POS_BACKUP
    LDY V_POS
    STY V_POS_BACKUP
    JSR MOVEMENT      ; convert bits set to movement
    JSR CLEAR_DRAW_BOX
  INC $D020

  INC $D020
  LDX #50
  LDY #50
  JSR BULLET_11
;  LDX #60
;  LDY #60
;  JSR BULLET_01
  LDX #70
  LDY #70
  JSR BULLET_10
  DEC $D020

  INC $D020
  LDX #100
  LDY #100
  JSR BOX_11
;  LDX #110
;  LDY #110
;  JSR BOX_01
  LDX #120
  LDY #120
  JSR BOX_10
  DEC $D020

  DEC $D020
 LDA V_POS
 LDX #0
 LDY #18
 JSR HEX8
 LDA H_POS
 LDX #0
 LDY #24
 JSR HEX8
 
  LDA REGA_INT
  LDX REGX_INT
  LDY REGY_INT
  INC $D019    ;VIC Interrupt Request Register (IRR)
  DEC $D020

IRQ_EXIT: 
NMI_NOP:
RTI ; This is the irq handler for the NMI. Just returns without acknowledge. This prevents subsequent NMI's from interfering.

!ZONE CLEAR_DRAW_BOX
CLEAR_DRAW_BOX
    LAX H_POS_BACKUP
    LDY V_POS_BACKUP
    CPY V_POS
    BNE .DRAW
    CPX H_POS
    BEQ .SAME
.DRAW JSR CLR_00 ; BOX_00_CLR
      LDY V_POS
      LDX H_POS
      JSR BOX_10 ; BOX_11
.SAME RTS

!ZONE CONVERT_MOVEMENT
CONVERT_MOVEMENT
    TXA
    BEQ .CONT
    BMI .NEGV
.POSV   LDA #1
      BNE .CONT ; JMP
.NEGV   LDA #2
.CONT TAX
    TYA
    BEQ .EXIT
    BMI .NEGH
.POSH   TXA
      ORA #4
      RTS
.NEGH   TXA
      ORA #8
      RTS
.EXIT TXA
    RTS

!ZONE MOVEMENT
MOVEMENT  ; pass in X/Y pos in A/Y and X holds direction and do movement and collision and returns position ix X/Y
  ASL       ;2
  STA .ADDR +1    ;4
.ADDR JMP (DIR_TAB) ;5

!ZONE BULLET_11
BULLET_11
  LDA BULLET_11_TAB,Y
  STA .ADDR +1
.ADDR JMP BULLET_11_01_07

!ZONE BULLET_10
BULLET_10
  LDA BULLET_11_TAB,Y
  STA .ADDR +1
.ADDR JMP BULLET_10_01_07

!ZONE BULLET_01
BULLET_01
  LDA BULLET_11_TAB,Y
  STA .ADDR +1
.ADDR JMP BULLET_01_01_07

!ZONE BOX_11
BOX_11
  LDA BOX_11_TAB,Y
  STA .ADDR +1
.ADDR JMP BOX_11_01_06

!ZONE BOX_10
BOX_10
  LDA BOX_11_TAB,Y
  STA .ADDR +1
.ADDR JMP BOX_10_01_06

!ZONE BOX_01
BOX_01
  LDA BOX_11_TAB,Y
  STA .ADDR +1
.ADDR JMP BOX_01_01_06

!ZONE CLR_00
CLR_00
  LDA CLR_00_TAB,Y
  STA .ADDR +1
.ADDR JMP CLR_00_01_06

!ZONE TEST_JMP
TEST_JMP
  LDA TEST_JMP,X  ;4
  STA .ADDR +1    ;4
.ADDR JMP TEST_JMP  ;3

!ZONE BULLET_01_01_07
BULLET_01_01_07
      LDA   SCR_TAB_HI,Y    ;4
      STA   SCN +1        ;3
      LDA   SCR_TAB_LO,Y    ;4
      STA   SCN         ;3

      LDY   HOR_TAB,X     ;4
      LDA   (SCN),Y       ;5
      ORA   OR_01_TAB,X     ;4
      STA   (SCN),Y       ;6

      LDY   HOR_TAB+1,X     ;4
      INY           ;2
      LDA   (SCN),Y       ;5
      ORA   OR_01_TAB+1,X   ;4
      STA   (SCN),Y       ;6

      RTS

!ZONE BULLET_01_08_08
BULLET_01_08_08
      LDA   SCR_TAB_HI,Y    ;4
      STA   SCN +1        ;3
      LDA   SCR_TAB_LO,Y    ;4
      STA   SCN         ;3

      LDA   SCR_TAB_HI+1,Y    ;4
      STA   DST_BOT +1      ;3
      LDA   SCR_TAB_LO+1,Y    ;4
      STA   DST_BOT       ;3

      LDY   HOR_TAB,X     ;2
      LDA   (SCN),Y       ;5
      ORA   OR_01_TAB,X     ;4
      STA   (SCN),Y       ;6

      LDY   HOR_TAB+1,X     ;4
      LDA   (DST_BOT),Y     ;5
      ORA   OR_01_TAB+1,X   ;4
      STA   (DST_BOT),Y     ;6

      RTS
        
RANDOM  LDA   EORN+1
        BEQ   EORY
        ASL
        BEQ   EORN ;if the input was $80, skip the EOR
        BCC   EORN
EORY      EOR   #$1D
EORN    STA   EORN+1
        RTS

HEX16 STY   REGY_BUF  ; Y = YPOS / A = HI / X = LO
      STX   REGX_BUF    
      LDX   #0
      JSR   HEX8
      LDA   REGX_BUF
      LDY   REGY_BUF
      LDX   #16
;      JSR   HEX8
;      RTS

HEX8 ; A = NUM / Y = YPOS / X = XPOS

      STA   REGA_BUF
      LDA   SCR_TAB_LO+0,Y
      STA   HEX_NUMBER0+1
      
      LDA   SCR_TAB_HI+0,Y
      STA   HEX_NUMBER0+2

      LDA   SCR_TAB_LO+1,Y
      STA   HEX_NUMBER1+1
      
      LDA   SCR_TAB_HI+1,Y 
      STA   HEX_NUMBER1+2

      LDA   SCR_TAB_LO+2,Y
      STA   HEX_NUMBER2+1
      
      LDA   SCR_TAB_HI+2,Y 
      STA   HEX_NUMBER2+2

      LDA   SCR_TAB_LO+3,Y
      STA   HEX_NUMBER3+1
      
      LDA   SCR_TAB_HI+3,Y 
      STA   HEX_NUMBER3+2

      LDA   SCR_TAB_LO+4,Y
      STA   HEX_NUMBER4+1
      
      LDA   SCR_TAB_HI+4,Y 
      STA   HEX_NUMBER4+2

      LDA   REGA_BUF
      LSR
      LSR
      LSR
      LSR
      TAY
      JSR   HEXCHAR

      TXA
      CLC
      ADC   #8
      TAX

      LDA   REGA_BUF
      AND   #15
      TAY
;      JSR   HEXCHAR
;      RTS

HEXCHAR     LDA   HEX0,Y
HEX_NUMBER0 STA   $ABCD,X
      LDA   HEX1,Y
HEX_NUMBER1 STA   $ABCD,X
      LDA   HEX2,Y
HEX_NUMBER2 STA   $ABCD,X
      LDA   HEX3,Y
HEX_NUMBER3 STA   $ABCD,X
      LDA   HEX4,Y
HEX_NUMBER4 STA   $ABCD,X
      RTS

!ZONE
SCN_SETUP
 LDA #BLACK ;$65 ; colour 01 (0000????) and colour 10 (????0000) - bank 1
 LDX #<(SCRN)
 LDY #>(SCRN)
 STX SCN+0
 STY SCN+1
 LDX #>8000 
 LDY #<8000
 JSR MEMSET

  LDX #0
  JSR DRAW_ROOM

  RTS

!ZONE DRAW_ROOM
DRAW_ROOM  ;x=room no
  LDA ROOM_CNT_TAB,X
  STA ROOM_CNT

  LDA ROOM_START_H_LO,X
  STA ROOM_START_H
  LDA ROOM_START_H_HI,X
  STA ROOM_START_H +1
  LDA ROOM_START_V_LO,X
  STA ROOM_START_V
  LDA ROOM_START_V_HI,X
  STA ROOM_START_V +1
  LDA ROOM_END_HV_LO,X
  STA ROOM_END_HV
  LDA ROOM_END_HV_HI,X
  STA ROOM_END_HV +1
  LDA ROOM_DIR_LO,X
  STA ROOM_DIR
  LDA ROOM_DIR_HI,X
  STA ROOM_DIR +1
  LDA #0
  STA ROOM_POS
.LOOP
    LDY ROOM_POS
    LDA (ROOM_START_H),Y
    STA ROOM_ST_H_VAL
    LDA (ROOM_START_V),Y
    STA ROOM_ST_V_VAL
    LDA (ROOM_END_HV),Y
    STA ROOM_END_HV_VAL
    LDA (ROOM_DIR),Y
    BEQ .HOR_LINE
.VER_LINE
      LDX ROOM_ST_H_VAL
      LDY ROOM_ST_V_VAL
      LDA ROOM_END_HV_VAL
      JSR ROOM_VER
      JMP .CONT
.HOR_LINE
      LDX ROOM_ST_H_VAL
      LDY ROOM_ST_V_VAL
      LDA ROOM_END_HV_VAL
      JSR ROOM_HOR
.CONT
    INC ROOM_POS
    DEC ROOM_CNT
    BPL .LOOP
  RTS

!ZONE ROOM_HOR  ; x=starth y=startv a=endh
ROOM_HOR
  STY START_V
  STA END_VH
  STX START_H ; store start
  SEC
  SBC START_H ; en - st = distance
  STA START_H
.LOOP
    LDX END_VH
    LDY START_V
    JSR BOX_11
    DEC END_VH
    DEC START_H
    BPL .LOOP
  RTS

!ZONE ROOM_VER  ; x=starth y=startv a=endh
ROOM_VER
  STX START_H
  STA END_VH
  STY START_V
  SEC
  SBC START_V
  LSR
  STA START_V
.LOOP
    LDX START_H
    LDY END_VH
    JSR BOX_11
    DEC END_VH
    DEC END_VH
    DEC START_V
    BPL .LOOP
  RTS

HEX0  !BYTE %00111111,%00001100,%00111111,%00111111,%00110011,%00111111,%00111111,%00111111,%00111111,%00111111,%00111111,%00111111,%00111111,%00111100,%00111111,%00111111 
HEX1  !BYTE %00110011,%00111100,%00000011,%00000011,%00110011,%00110000,%00110000,%00000011,%00110011,%00110011,%00110011,%00110011,%00110011,%00110011,%00110000,%00110000 
HEX2  !BYTE %00110011,%00001100,%00111111,%00111111,%00111111,%00111111,%00111111,%00000011,%00111111,%00111111,%00111111,%00111100,%00110000,%00110011,%00111111,%00111111
HEX3  !BYTE %00110011,%00001100,%00110000,%00000011,%00000011,%00000011,%00110011,%00000011,%00110011,%00000011,%00110011,%00110011,%00110011,%00110011,%00110000,%00110000
HEX4  !BYTE %00111111,%00111111,%00111111,%00111111,%00000011,%00111111,%00111111,%00000011,%00111111,%00111111,%00110011,%00111111,%00111111,%00111100,%00111111,%00110000

ROOM_CNT_TAB    !BYTE 3
ROOM_START_H_LO !BYTE <ROOM_00_START_H
ROOM_START_H_HI !BYTE >ROOM_00_START_H
ROOM_START_V_LO !BYTE <ROOM_00_START_V
ROOM_START_V_HI !BYTE >ROOM_00_START_V
ROOM_END_HV_LO  !BYTE <ROOM_00_END_HV
ROOM_END_HV_HI  !BYTE >ROOM_00_END_HV
ROOM_DIR_LO     !BYTE <ROOM_00_DIR
ROOM_DIR_HI     !BYTE >ROOM_00_DIR

ROOM_00_START_H !BYTE 0,0,0,126
ROOM_00_START_V !BYTE 0,0,196,0
ROOM_00_END_HV  !BYTE 126,196,126,196
ROOM_00_DIR     !BYTE 0,1,0,1

!ALIGN 255,0
DIR_TAB
      !WORD DIR_ER ; 0000
      !WORD DIR_RT ; 0001
      !WORD DIR_LT ; 0010
      !WORD DIR_ER ; 0011
      !WORD DIR_DN ; 0100
      !WORD DIR_DR ; 0101
      !WORD DIR_DL ; 0110
      !WORD DIR_ER ; 0111
      !WORD DIR_UP ; 1000
      !WORD DIR_UR ; 1001
      !WORD DIR_UL ; 1010
      !WORD DIR_ER ; 1011
      !WORD DIR_ER ; 1100
      !WORD DIR_ER ; 1101
      !WORD DIR_ER ; 1110
      !WORD DIR_ER ; 1111

SCR_TAB_HI
!for I = 0 TO 24
!BYTE >(SCRN+(I*320)+0),>(SCRN+(I*320)+1),>(SCRN+(I*320)+2),>(SCRN+(I*320)+3),>(SCRN+(I*320)+4),>(SCRN+(I*320)+5),>(SCRN+(I*320)+6),>(SCRN+(I*320)+7)
!end

!ALIGN 255,0
SCR_TAB_LO
!for I = 0 TO 24
!BYTE <(SCRN+(I*320)+0),<(SCRN+(I*320)+1),<(SCRN+(I*320)+2),<(SCRN+(I*320)+3),<(SCRN+(I*320)+4),<(SCRN+(I*320)+5),<(SCRN+(I*320)+6),<(SCRN+(I*320)+7)
!end

!ALIGN 255,0
OR_11_TAB:
!for I = 1 TO 32
!BYTE %11000000,%00110000,%00001100,%00000011
!end

;!ALIGN 255,0
OR_10_TAB:
!for I = 1 TO 32
!BYTE %10000000,%00100000,%00001000,%00000010
!end

;!ALIGN 255,0
OR_01_TAB:
!for I = 1 TO 32
!BYTE %01000000,%00010000,%00000100,%00000001
!end

AND_00_TAB:
!for I = 1 TO 32
!BYTE %00111111,%11001111,%11110011,%11111100
!end

HOR_TAB:
!FILL 4,00*8
!FILL 4,01*8
!FILL 4,02*8
!FILL 4,03*8
!FILL 4,04*8
!FILL 4,05*8
!FILL 4,06*8
!FILL 4,07*8
!FILL 4,08*8
!FILL 4,09*8
!FILL 4,10*8
!FILL 4,11*8
!FILL 4,12*8
!FILL 4,13*8
!FILL 4,14*8
!FILL 4,15*8
!FILL 4,16*8
!FILL 4,17*8
!FILL 4,18*8
!FILL 4,19*8
!FILL 4,20*8
!FILL 4,21*8
!FILL 4,22*8
!FILL 4,23*8
!FILL 4,24*8
!FILL 4,25*8
!FILL 4,26*8
!FILL 4,27*8
!FILL 4,28*8
!FILL 4,29*8
!FILL 4,30*8
!FILL 4,31*8

BOX_11_TAB
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08
      !BYTE <BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_01_06,<BOX_11_07_08,<BOX_11_08_08

CLR_00_TAB
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08
      !BYTE <CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_01_06,<CLR_00_07_08,<CLR_00_08_08

BULLET_11_TAB
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08

      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08

      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08

      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08

      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
      !BYTE <BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_01_07,<BULLET_11_08_08
    

; x=startv y=starth a=endv
; draw ver wall line
; for x eq startv to endv plotwall xtartv,starth

; x=startv y=starth a=endh
; draw hor wall line
; for y eq starth to endh plotwall xtartv,starth

; 0000

; multicolour with shadow
; P
; PS
;  S

; SRC_TOP
; SRC_MID
; SRC_BOT

; DST_TOP
; DST_MID
; DST_BOT

; P
; PS
;  S

; draw_obj
; set DST_TOP
; set DST_MID
; set DST_BOT
; plot DST_TOP+0
; plot DST_MID+0
; test DST_MID+1
;  plot DST_MID+1
; test DST_BOT+1
;  plot DST_BOT+1

; P
; PS
;  S

; clear_obj
; set DST_TOP + SRC_TOP (by ORA bit)
; set DST_MID + SRC_MID (by ORA bit)
; set DST_BOT + SRC_BOT (by ORA bit)
; ldy HOR,X
; lda SRC_TOP,Y / sta DST_TOP,Y
; lda SRC_MID,Y / sta DST_MID,Y
; ldy HOR+1,X
; lda SRC_MID,Y / sta DST_MID,Y
; lda SRC_BOT,Y / sta DST_BOT,Y

; B
;  S

; draw_bullet
; set DST_TOP
; set DST_MID
; plot DST_TOP+0
; test DST_MID+1
;  plot DST_MID+1

; B
;  S

; clear bullet
; set DST_TOP + SRC_TOP (by ORA bit)
; set DST_MID + SRC_MID (by ORA bit)
; ldy HOR,X
; lda SRC_TOP,Y / sta DST_TOP,Y
; ldy HOR+1,X
; lda SRC_MID,Y / sta DST_MID,Y
